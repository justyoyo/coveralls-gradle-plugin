// build.gradle

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'signing'
apply plugin: 'cobertura'

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    compile 'com.android.tools.build:gradle:0.11.+'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'org.apache.httpcomponents:httpmime:4.3'

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.github.tomakehurst:wiremock:1.18'
}

buildscript {
    repositories {
        mavenCentral()

        maven {
            url 'http://kt3k.github.io/repository/maven/release'
        }
    }

    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:1.1.2'
    }
}


task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives jar

    archives groovydocJar
    archives sourcesJar
}


// sign archives if signatory exists
if (signing.signatory) {

    signing {
        sign configurations.archives
    }

}


uploadArchives {
    repositories {
        mavenDeployer {

            // sign pom if signatory exists
            if (signing.signatory) {
                beforeDeployment {
                    signing.signPom(it)
                }
            }

            repository(url: System.getenv('MVN_REPO')) {

                // set authentication info if sonatypeUsername property is set
                if (project.hasProperty('sonatypeUsername')) {
                    authentication userName: sonatypeUsername, password: sonatypePassword
                }
            }

            // additional pom settings (for maven central requirement)
            pom.project {

                name 'Gradle Coveralls Plugin'
                description 'This plugin helps to upload coverage data to Coveralls'
                url 'https://github.com/kt3k/coveralls-gradle-plugin'

                scm {
                    url 'scm:git@github.com:kt3k/coveralls-gradle-plugin.git'
                    connection 'scm:git@github.com:kt3k/coveralls-gradle-plugin.git'
                    developerConnection 'scm:git@github.com:kt3k/coveralls-gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://raw.github.com/kt3k/coveralls-gradle-plugin/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'kt3k'
                        name 'Yoshiya Hinosawa'
                    }
                }
            }
        }
    }
}

cobertura.coverageFormats = ['html', 'xml']
cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs
